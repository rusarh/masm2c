?start: _directivelist?  enddir
%ignore " "

U_DOT: "."
_COMMA: ","
charlist: /[^\r\n\t ]+/x
text: /[^\r\n]+/x
STRING: /((?<!')'(?!').+?(?<!')'(?!'))|((?<!")"(?!").+?(?<!")"(?!"))/
macroname: "macroname"
structname: "structname"
!label: /[A-Za-z@_$?][A-Za-z@_$?0-9]*/
//MONKEY: /@@|@B|@F/

COMMENTKW: /comment\s+(\S)/ //[.\r\n]*?\1[^\r\n]*/x
_ENDOFLINE: /[\r\n]+[\t \r\n]*/x
//FLOAT: /-?[0-9]*\.?[0-9]+([eE][-+]?[0-9]+)?/
integer: /[0-9][0-9A-Fa-f]*[HOQTYhoqty]?/
WS: /([\t \b\v\f\x1a]+)|(\\;?[^\r\n]*[\r\n\t \b\v\f\x1a]+)/x
SPACE: /[\t ]+/
!comment: /;[^\r\n]*/x
KEYWORD: /\w+/
arbitrarytext: /[^\r\n]+/x
mnemonic: "AAA"i|"AAD"i|"AAM"i|"AAS"i|"ADC"i|"ADD"i|"AND"i|"CALL"i|"CBW"i|"CLC"i|"CLD"i|"CLI"i|"CMC"i|"CMP"i|"CMPS"i|"CMPSB"i|"CMPSW"i|"CMPXCHG8B"i|"CWD"i|"DAA"i|"DAS"i|"DEC"i|"DIV"i|"ESC"i|"HLT"i|"IDIV"i|"IMUL"i|"IN"i|"INC"i|"INT"i|"INTO"i|"IRET"i|"JA"i|"JAE"i|"JB"i|"JBE"i|"JC"i|"JCXZ"i|"JE"i|"JG"i|"JGE"i|"JL"i|"JLE"i|"JMP"i|"JNA"i|"JNAE"i|"JNB"i|"JNBE"i|"JNC"i|"JNE"i|"JNG"i|"JNGE"i|"JNL"i|"JNLE"i|"JNO"i|"JNP"i|"JNS"i|"JNZ"i|"JO"i|"JP"i|"JPE"i|"JPO"i|"JS"i|"JZ"i|"LAHF"i|"LDS"i|"LEA"i|"LES"i|"LODS"i|"LODSB"i|"LODSW"i|"LOOP"i|"LOOPE"i|"LOOPEW"i|"LOOPNE"i|"LOOPNEW"i|"LOOPNZ"i|"LOOPNZW"i|"LOOPW"i|"LOOPZ"i|"LOOPZW"i|"MOV"i|"MOVS"i|"MOVSB"i|"MOVSW"i|"MUL"i|"NEG"i|"NOP"i|"NOT"i|"OR"i|"OUT"i|"POP"i|"POPF"i|"PUSH"i|"PUSHF"i|"RCL"i|"RCR"i|"RET"i|"RETF"i|"RETN"i|"ROL"i|"ROR"i|"SAHF"i|"SAL"i|"SAR"i|"SBB"i|"SCAS"i|"SCASB"i|"SCASW"i|"SHL"i|"SHR"i|"STC"i|"STD"i|"STI"i|"STOS"i|"STOSB"i|"STOSW"i|"SUB"i|"TEST"i|"WAIT"i|"XCHG"i|"XLAT"i|"XLATB"i|"XOR"i|"BOUND"i|"ENTER"i|"INS"i|"INSB"i|"INSW"i|"LEAVE"i|"OUTS"i|"OUTSB"i|"OUTSW"i|"POPA"i|"PUSHA"i|"PUSHW"i|"BSF"i|"BSR"i|"BT"i|"BTC"i|"BTR"i|"BTS"i|"CDQ"i|"CMPSD"i|"CWDE"i|"INSD"i|"IRETD"i|"IRETDF"i|"IRETF"i|"JECXZ"i|"LFS"i|"LGS"i|"LODSD"i|"LOOPD"i|"LOOPED"i|"LOOPNED"i|"LOOPNZD"i|"LOOPZD"i|"LSS"i|"MOVSD"i|"MOVSX"i|"MOVZX"i|"OUTSD"i|"POPAD"i|"POPFD"i|"PUSHAD"i|"PUSHD"i|"PUSHFD"i|"SCASD"i|"SETA"i|"SETAE"i|"SETB"i|"SETBE"i|"SETC"i|"SETE"i|"SETG"i|"SETGE"i|"SETL"i|"SETLE"i|"SETNA"i|"SETNAE"i|"SETNB"i|"SETNBE"i|"SETNC"i|"SETNE"i|"SETNG"i|"SETNGE"i|"SETNL"i|"SETNLE"i|"SETNO"i|"SETNP"i|"SETNS"i|"SETNZ"i|"SETO"i|"SETP"i|"SETPE"i|"SETPO"i|"SETS"i|"SETZ"i|"SHLD"i|"SHRD"i|"STOSD"i|"BSWAP"i|"CMPXCHG"i|"XADD"i
//| F2XM1| FABS| FADD| FADDP| FBLD| FBSTP| FCHS| FCLEX| FCOM| FCOMP| FCOMPP| FDECSTP| FDISI| FDIV| FDIVP| FDIVR| FDIVRP
//| FENI| FFREE| FIADD| FICOM| FICOMP| FIDIV| FIDIVR| FILD| FIMUL| FINCSTP| FINIT| FIST| FISTP| FISUB| FISUBR| FLD| FLD1
//| FLDCW| FLDENV| FLDENVW| FLDL2E| FLDL2T| FLDLG2| FLDLN2| FLDPI| FLDZ| FMUL| FMULP| FNCLEX| FNDISI| FNENI| FNINIT
//| FNOP| FNSAVE| FNSAVEW| FNSTCW| FNSTENV| FNSTENVW| FNSTSW| FPATAN| FPREM| FPTAN| FRNDINT| FRSTOR| FRSTORW| FSAVE
//| FSAVEW| FSCALE| FSQRT| FST| FSTCW| FSTENV| FSTENVW| FSTP| FSTSW| FSUB| FSUBP| FSUBR| FSUBRP| FTST| FWAIT| FXAM| FXCH
//| FXTRACT| FYL2X| FYL2XP1| FSETPM| FCOS| FLDENVD| FNSAVED| FNSTENVD| FPREM1| FRSTORD| FSAVED| FSIN| FSINCOS| FSTENVD| FUCOM
//| FUCOMP| FUCOMPP
//
//datadecl: /(?i)(DB) | (DW) | (DD) | (DF) | (DQ) | (DT) | (BYTE)|(SBYTE)|(WORD)|(SWORD)|(DWORD)|(SDWORD)|(FWORD)|(QWORD)|(TBYTE)|(REAL4)|(REAL8)|(REAL10)/
!datadecl: "DB"i | "DW"i | "DD"i | "DF"i | "DQ"i | "DT"i | "BYTE"i | "SBYTE"i|"WORD"i|"SWORD"i|"DWORD"i|"SDWORD"i|"FWORD"i|"QWORD"i|"TBYTE"i|"REAL4"i|"REAL8"i|"REAL10"i
!datatype: "BYTE"i|"SBYTE"i|"WORD"i|"SWORD"i|"DWORD"i|"SDWORD"i|"FWORD"i|"QWORD"i|"TBYTE"i|"REAL4"i|"REAL8"i|"REAL10"i
!register: "AX"i|"EAX"i|"BX"i|"EBX"i|"CX"i|"ECX"i|"DX"i|"EDX"i|"BP"i|"EBP"i|"SP"i|"ESP"i|"DI"i|"EDI"i|"SI"i|"ESI"i|"AL"i|"AH"i|"BL"i|"BH"i|"CL"i|"CH"i|"DL"i|"DH"i
!segmentregister: "CS"i|"DS"i|"ES"i|"FS"i|"GS"i|"SS"i
!specialregister: "CR0"i|"CR2"i|"CR3"i|"DR0"i|"DR1"i|"DR2"i|"DR3"i|"DR6"i|"DR7"i|"TR3"i|"TR4"i|"TR5"i|"TR6"i|"TR7"i
!instrprefix: "REP"i|"REPE"i|"REPZ"i|"REPNE"i|"REPNZ"i|"LOCK"i
!langtype: "C"i|"PASCAL"i|"FORTRAN"i|"BASIC"i|"SYSCALL"i|"STDCALL"i
!relop: "EQ"i|"NE"i|"LT"i|"LE"i|"GT"i|"GE"i
!maptype: "ALL"i|"NONE"i|"NOTPUBLIC"i
!memoption: "TINY"i|"SMALL"i|"MEDIUM"i|"COMPACT"i|"LARGE"i|"HUGE"i|"FLAT"i
!listoption: ".LIST"i|".NOLIST"i|".XLIST"i|".LISTALL"i|".LISTIF"i|".LFCOND"i|".NOLISTIF"i|".SFCOND"i|".TFCOND"i|".LISTMACROALL"i|".LALL"i|".NOLISTMACRO"i|".SALL"i|".LISTMACRO"i|".XALL"i
PROCESSOR: ".8086"i|".186"i|".286"i|".286C"i|".286P"i|".386"i|".386C"i|".386P"i|".486"i|".486P"i|".586"i|".586P"i|".686"i|".686P"i|".K3D"i|".MMX"i|".XMM"i
!coprocessor: ".8087"i|".287"i|".387"i|".NO87"i
!segalign: "BYTE"i|"WORD"i|"DWORD"i|"PARA"i|"PAGE"i
!segorderdir: ".ALPHA"i|".SEQ"i|".DOSSEG"i|"DOSSEG"i
ASSUMENOTHING: "ASSUME NOTHING"i

aliasdir: "ALIAS"i
altidq: "(" label ")"
assdir: label "=" _expr _linereminder
assumedir: "ASSUME"i assumelist _linereminder| ASSUMENOTHING _linereminder
assumelist : assumeregister| assumeregister _COMMA assumelist
assumereg: register ":" assumeval
assumeregister: assumesegreg| assumereg
assumesegreg: segmentregister ":" assumesegval
assumesegval: "NOTHING"i | "ERROR"i | frameexpr
assumeval: "NOTHING"i | "ERROR"i | qualifiedtype
//bcdconst: _unadd?  decnumber
binaryop: "=="| "!="| ">="| "<="| ">"| "<"| "&"
//bitdef: bitfieldid ":" _expr eqconstexpr?
bitdef: bitfieldid ":" _expr
bitdeflist: bitdef | bitdef commaoeol bitdeflist
bitfieldid : label
blockstatements: ".CONTINUE"i ifcexpr? _linereminder | ".BREAK"i ifcexpr? _linereminder| _insegdir+
bool: "TRUE"i | "FALSE"i
brmacroarglist: "<" macroarglist ">"
classname: STRING
commaidvararg: commaoeol   label? "VARARG"i
commainvokelist: commaoeol invokelist
commamodeloptlist: _COMMA modeloptlist
commanonuniq: _COMMA "NONUNIQUE"i
commapagewidth: _COMMA _expr
commaparmidvararg: commaoeol  label "VARARG"i
commaparmlist: commaoeol? parmlist
commaprotolist: commaoeol  protolist
commatextlen: _COMMA _expr
commdecl: nearfar?   langtype?  label ":" commtype eqconstexpr?
commdir: "COMM"i commlist _linereminder
//commentdir: COMMENTKW DELIMITER text text DELIMITER text _linereminder; TODO
commentdir: COMMENTKW _linereminder
commlist: commdecl | commdecl _COMMA commlist
commtype: _expr
//constant: digits  radixoverride?
contextdir: "PUSHCONTEXT"i contextitemlist _linereminder| "POPCONTEXT"i contextitemlist _linereminder
contextitem: "ASSUMES"i | "RADIX"i | "LISTING"i | "CPU"i | "ALL"i
contextitemlist: contextitem | contextitem _COMMA contextitemlist
controlblock: whileblock| repeatblock
controldir: controlif| controlblock
controlelseif: ".ELSEIF"i cexpr _linereminder _insegdir+ controlelseif?
controlif: ".IF"i cexpr _linereminder _insegdir+ controlelseif? elsedirectivelist? ".ENDIF"i _linereminder
crefdir: crefoption _linereminder
crefoption: ".CREF"i | ".XCREF"i idlist? | ".NOCREF"i idlist?
//datadecl: DB | DW | DD | DF | DQ | DT | datatype
//datadir: label? dataitem _linereminder
datadir: label? datadecl scalarinstlist _linereminder | structinstdir | recordinstdir
//datadir: label? datadecl scalarinstlist _linereminder | label? label structinstlist _linereminder
//datadir: label? datadecl scalarinstlist _linereminder
structinstdir: label? structname structinstlist _linereminder
recordinstdir: label? label recordinstlist _linereminder
//dataitem: datadecl scalarinstlist| label structinstlist| label recordinstlist
decnumber: integer
digits: decnumber
_directive: _generaldir| _segmentdef | _linereminder
_directivelist: _directive+
distance: nearfar| "NEAR16"i | "NEAR32"i | "FAR16"i | "FAR32"i
dotdotforparmtype: ":" forparmtype
dotdotparmtype: ":" parmtype
dotdotqualifiedtype: ":" qualifiedtype

// Precedence Operators
// 1 ( ), [ ]
// 2 LENGTH , SIZE , WIDTH , MASK, LENGTHOF, SIZEOF
// 3 . (structure-field-name operator)
// 4 : (segment-override operator), PTR
// 5 LROFFSET , OFFSET , SEG , THIS , TYPE
// 6 HIGH , HIGHWORD , LOW , LOWWORD
// 7 +, - (unary)
// 8 *, /, MOD , SHL , SHR
// 9 +, - (binary)
// 10 EQ , NE , LT , LE , GT , GE
// 11 NOT
// 12 AND
// 13 OR , XOR
// 14 OPATTR , SHORT , .TYPE
cexpr: aexpr "||" cexpr | aexpr
aexpr: term "&&" aexpr | term
term: "!" simpleexpr | simpleexpr
simpleexpr: "(" cexpr ")"| primary
primary: _expr binaryop _expr| flagname| _expr
cxzexpr: "!" _expr| _expr  "==" _expr| _expr  "!=" _expr | _expr

sqexpr: "[" _expr "]"
//sqexprlist: label? sqexpr sqexpr?
offsetdirmy: "OFFSET"i _e10
segmdir: "SEG"i _e10
segoverride: _e09 ":" _e10
ptrdir: _e09 "PTR"i _e10
| "LARGE"i _e10
| "SMALL"i _e10
| "SHORT"i _e10
ordir: _e01 "OR"i _e02
xordir: _e01 "XOR"i _e02
anddir: _e02 "AND"i _e03
notdir: "NOT"i _e04
!sign: ("+"|"-")
addop: _e05 sign _e06
_unadd: sign _e08

field: "." _e11
fieldlist: field+
memberdir: _e10 fieldlist

_e01: ordir | xordir
| _e02
_e02: anddir
| _e03
_e03: notdir
| _e04
_e04: _e04 relop _e05
| _e05
_e05: addop
| _e06
_e06: _e06 mulop _e07
| _e06 shiftop _e07
| _e07
_e07: _unadd
| _e08
_e08: "HIGH"i _e09
| "LOW"i _e09
| "HIGHWORD"i _e09
| "LOWWORD"i _e09
| _e09
_e09: offsetdirmy
| segmdir
| "LROFFSET"i _e10
| "TYPE"i _e10
| "THIS"i _e10
| ptrdir
| segoverride //| _e09 ":" _e10
| _e10
_e10: memberdir
| _e10 _expr
| _e11
dir3: "WIDTH"i label
| "MASK"i label
| "SIZE"i sizearg
| "SIZEOF"i sizearg
| "LENGTH"i label
| "LENGTHOF"i label
_e11: "(" _expr ")"
| sqexpr
| dir3
| recordconst
| STRING
| integer
| type
| "$"
| _anyregister
| "ST"i
| "ST"i "(" _expr ")"
_expr: ".TYPE"i _e01
| "OPATTR"i _e01
| _e01
sizearg: _e10

echodir: "ECHO"i arbitrarytext _linereminder | ".OUT"i arbitrarytext _linereminder
elsedirectivelist: ".ELSE"i _linereminder _insegdir+
elseifblock: elseifstatement _linereminder _insegdir+ elseifblock?
elseifstatement: "ELSEIF"i _expr| "ELSEIFE"i _expr| "ELSEIFB"i textitem| "ELSEIFNB"i textitem | "ELSEIFDEF"i label| "ELSEIFNDEF"i label| "ELSEIFDIF"i textitem _COMMA textitem| "ELSEIFDIFI"i textitem _COMMA textitem| "ELSEIFIDN"i textitem _COMMA textitem| "ELSEIFIDNI"i textitem _COMMA textitem| "ELSEIF1"i | "ELSEIF2"i
enddir: "END"i _expr?  _linereminder?
endpdir: label "ENDP"i _linereminder
endsdir: label "ENDS"i _linereminder
eqconstexpr: "=" _expr
equdir: label "EQU"i equtype _linereminder
equtype: _expr| textliteral
errordir: erroropt _linereminder
erroropt: ".ERR"i textitem? | ".ERRE"i _expr opttext? | ".ERRNZ"i _expr opttext? | ".ERRB"i textitem opttext? | ".ERRNB"i textitem opttext? | ".ERRDEF"i label  opttext? | ".ERRNDEF"i label opttext? | ".ERRDIF"i textitem _COMMA textitem  opttext? | ".ERRDIFI"i textitem _COMMA textitem  opttext? | ".ERRIDN"i textitem _COMMA textitem  opttext? | ".ERRIDNI"i textitem _COMMA textitem  opttext? | ".ERR1"i textitem? | ".ERR2"i textitem?
exitdir: ".EXIT"i _expr?  _linereminder
exitmdir: "EXITM"i textitem? _linereminder
exprlist: _expr (_COMMA _expr)*
exprspacelist: _expr+
externdef: langtype?  label altidq? ":" externtype
externdir: externkey externlist _linereminder
externkey: "EXTRN"i | "EXTERN"i | "EXTERNDEF"i
externlist: externdef | externdef commaoeol externlist
externtype: "ABS"i | qualifiedtype
fieldinit: initvalue| structinstance | _expr
fieldinitlist: fieldinit | fieldinit commaoeol fieldinitlist
filespec: charlist| textliteral
flagname: "ZERO?"i | "CARRY?"i | "OVERFLOW?"i | "SIGN?"i | "PARITY?"i
floatnumber : SIGNED_FLOAT | digits "R"| digits "r"
forcdir: "FORC"i | "IRPC"i
fordir: "FOR"i | "IRP"i
forparm: label dotdotforparmtype?
forparmtype: "REQ"i | "=" textliteral
?frameexpr: "SEG"i label | "DGROUP"i ":" label| segmentregister ":" label| label
_generaldir: modeldir | segorderdir | namedir| includelibdir | commentdir| groupdir | assumedir| structdir | typedefdir| externdir | publicdir | commdir | prototypedir| equdir | assdir | textdir| contextdir | optiondir | processordir| radixdir | titledir | pagedir | listdir | crefdir | echodir| ifdir | errordir | includedir | macrodir | macrocall | macrorepeat | purgedir| macrowhile | macrofor | macroforc| aliasdir | recorddir | smartdir
groupdir : label "GROUP"i segidlist
idlist: label | label _COMMA idlist
ifcexpr: ".IF"i cexpr
ifdir: ifstatement _linereminder _insegdir+ elseifblock? elsedirectivelist? "ENDIF"i _linereminder
ifstatement: "IF"i _expr| "IFE"i _expr| "IFB"i textitem| "IFNB"i textitem| "IFDEF"i label| "IFNDEF"i label| "IFDIF"i textitem _COMMA textitem| "IFDIFI"i textitem _COMMA textitem| "IFIDN"i textitem _COMMA textitem| "IFIDNI"i textitem _COMMA textitem| "IF1"i | "IF2"i
includedir: "INCLUDE"i filespec _linereminder
includelibdir: "INCLUDELIB"i filespec _linereminder
dupdir: _expr "DUP"i "(" scalarinstlist ")"
initvalue: "?"| dupdir | floatnumber
_insegdir: labeldef _insegmentdir | _insegmentdir
//insegdirlist: _insegdir+
_insegmentdir: instruction| datadir| controldir| startupdir| exitdir| offsetdir| labeldir| procdir  localdirlist? endpdir| procdir  localdirlist? _insegdir+ endpdir| invokedir| _generaldir | _linereminder

asminstruction: instrprefix? mnemonic  exprlist?
//| PUSH allreglist
//| POP allreglist

instruction: asminstruction _linereminder

invokearg: _anyregister "::" register| "ADDR"i _expr | _expr
invokedir: "INVOKE"i _expr commainvokelist? _linereminder
invokelist: invokearg | invokearg commaoeol invokelist
labeldef: label ":" | label "::"
labeldir: label "label"i qualifiedtype _linereminder
_linereminder: _ENDOFLINE | comment _ENDOFLINE
listdir: listoption _linereminder
localdef: "LOCAL"i idlist _linereminder
localdir: "LOCAL"i parmlist _linereminder
localdirlist: localdir+
locallist: localdef+
macroarg: "%" _expr| "%" label "(" macroarglist ")" | "<" arbitrarytext ">" | arbitrarytext | STRING
macroarglist: macroarg | macroarg _COMMA macroarglist
macrobody: locallist? macrostmtlist
macrocall: macroname  "(" macroarglist ")" | macroname macroarglist? _linereminder
macrodirhead: label "MACRO"i macroparmlist?
macrodir: macrodirhead _linereminder macrobody "ENDM"i _linereminder
macrofor: fordir forparm  _COMMA "<" macroarglist ">" _linereminder macrobody "ENDM"i _linereminder
macroforc: forcdir label  _COMMA textliteral _linereminder macrobody "ENDM"i _linereminder
macronamelist: label | label _COMMA macronamelist
macroparm: label dotdotparmtype?
macroparmlist: macroparm | macroparm commaoeol macroparmlist
macrorepeat: repeatbegin _linereminder macrobody "ENDM"i _linereminder
repeatbegin: repeatdir _expr
macrostmt: instruction | labeldef | _directive| exitmdir| ":" label _linereminder| "GOTO"i label _linereminder
macrostmtlist: macrostmt+
macrowhile: "WHILE"i _expr _linereminder macrobody "ENDM"i _linereminder

modeldir: ".MODEL"i memoption commamodeloptlist? _linereminder
modelopt: langtype| stackoption | "OS_DOS"i
modeloptlist: modelopt | modelopt _COMMA modeloptlist
mulop: "*"| "/" | "MOD"i
namedir: "NAME"i label _linereminder
nearfar: "NEAR"i | "FAR"i
nestedstruct: structkw  label?  _linereminder "ENDS"i _linereminder | structkw  label?  _linereminder structitem+ "ENDS"i _linereminder
offsetdir: offsetdirtype _linereminder
offsetdirtype: "EVEN"i | "ORG"i _expr| "ALIGN"i _expr?
offsettype: "GROUP"i | "SEGMENT"i | "FLAT"i
optiondir: "OPTION"i optionlist _linereminder
optionitem: "CASEMAP"i ":" maptype| "DOTNAME"i | "NODOTNAME"i | "EMULATOR"i | "NOEMULATOR"i | "EPILOGUE"i ":" label| "EXPR16"i | "EXPR32"i | "LANGUAGE"i ":" langtype| "LJMP"i | "NOLJMP"i | "M510"i | "NOM510"i | "NOSIGNEXTEND"i | "OFFSET"i ":" offsettype| "OLDMACROS"i | "NOOLDMACROS"i | "OLDSTRUCTS"i | "NOOLDSTRUCTS"i | "PROC"i ":" ovisibility| "PROLOGUE"i ":" label| "READONLY"i | "NOREADONLY"i | "SCOPED"i | "NOSCOPED"i | "SEGMENT"i ":" segsize| "SETIF2"i ":" bool
optionlist: optionitem | optionitem commaoeol optionlist
opttext: _COMMA textitem
orop: "OR"i | "XOR"i
ovisibility: "PUBLIC"i | "PRIVATE"i | "EXPORT"i
pagedir: "PAGE"i pageexpr? _linereminder
pageexpr: "+"|  _expr commapagewidth? | commapagewidth
parm: label  _expr? dotdotqualifiedtype?
parmlist: parm | parm commaoeol parmlist
parmtype: "REQ"i | "=" textliteral| "VARARG"i
poptions: distance?  langtype?  ovisibility?
procdir: label "PROC"i poptions  brmacroarglist?  usesregs? procparmlist? commaparmidvararg? _linereminder
processordir: PROCESSOR _linereminder | coprocessor _linereminder
procparmlist: commaparmlist
protoarg : label? ":" qualifiedtype
protoarglist: commaprotolist
protolist: protoarg | protoarg commaoeol protolist
protospec: label | distance?   langtype? protoarglist? commaidvararg?
prototypedir: label "PROTO"i protospec
pubdef: langtype?  label
publicdir: "PUBLIC"i publist _linereminder
publist: pubdef | pubdef commaoeol publist
purgedir: "PURGE"i macronamelist
qualifiedtype: distance? "PTR"i qualifiedtype? | type
qualifier: "PROTO"i protospec | qualifiedtype
//QUOTE: "'"" | "\""
radixdir: ".RADIX"i _expr _linereminder
//readonly: READONLY
//oldrecordfieldlist: exprlist
recordconst_: "{" exprlist "}"| oldrecordinstance
oldrecordinstance: "<" exprlist ">"
recordconst: label recordconst_
recorddir: label "RECORD"i bitdeflist _linereminder
//recordfieldlist: _expr | _expr commaoeol recordfieldlist
recordfieldlist: scalarinstlist
recordinstance_: "{"  _linereminder? recordfieldlist _linereminder?  "}"
recordinstance: recordinstance_ | oldrecordinstance | _expr "DUP"i "(" recordinstance ")"
recordinstlist: recordinstance | recordinstance commaoeol recordinstlist
_anyregister: segmentregister | register | specialregister
reglist: register+
allreglist: _anyregister+

repeatblock: ".REPEAT"i _linereminder blockstatements untildir
repeatdir: "REPEAT"i | "REPT"i
scalarinstlist: initvalue | exprlist
commaoeol: _COMMA  _linereminder?
!segattrib: "PUBLIC"i | "STACK"i | "COMMON"i | "MEMORY"i | "AT"i _expr| "PRIVATE"i
segdir: ".CODE"i label? | ".DATA"i | ".DATA_"i| ".CONST"i | ".FARDATA"i label? | ".FARDATA_"i label? | ".STACK"i _expr?
segidlist : label  _COMMA segidlist | label
_segmentdef: segmentdir  endsdir|segmentdir  _insegdir+  endsdir| simplesegdir | simplesegdir  endsdir | simplesegdir  _insegdir+   endsdir?
segmentdir: label "SEGMENT"i segoptionlist?  _linereminder
_segoption: segalign| segro| segattrib| segsize| classname
segoptionlist: _segoption+
segro: "READONLY"i
!segsize : "USE16"i | "USE32"i | "FLAT"i
shiftop: "SHR"i | "SHL"i
// _unadd: "-" | "+"
simplesegdir: segdir _linereminder
stackoption: "NEARSTACK"i | "FARSTACK"i
startupdir: ".STARTUP"i _linereminder
structbody: structitem+
structdirhdr: label structkw  _expr?  commanonuniq?
structdir: structdirhdr _linereminder endsdir | structdirhdr _linereminder structitem+ endsdir
structkw: "STRUC"i | "STRUCT"i | "UNION"i
structdup: _expr "DUP"i "(" structinstlist ")"
structinstance: "<"  fieldinitlist?  ">"| "{" _linereminder?   fieldinitlist?   _linereminder?  "}"| structdup
structinstlist: structinstance | structinstance commaoeol structinstlist
structitem: datadir| _generaldir| offsetdir| nestedstruct| _linereminder
textdir: label textmacrodir _linereminder
textitem: textliteral| "%" _expr //| label
textlist: textitem | textitem commaoeol textlist
textliteral: "<" text ">" _linereminder
textmacrodir: "CATSTR"i textlist? | "TEXTEQU"i textlist? | "SIZESTR"i textitem| "SUBSTR"i textitem  _COMMA _expr commatextlen| "INSTR"i textstartcomma? textitem  _COMMA textitem
textstartcomma: _expr  _COMMA
titledir: titletype arbitrarytext _linereminder
titletype: "TITLE"i | "SUBTITLE"i | "SUBTTL"i
?type: label| distance| datadecl
typedefdir: label "TYPEDEF"i qualifier
untildir: ".UNTIL"i cexpr _linereminder | ".UNTILCXZ"i cxzexpr?  _linereminder
usesregs: "USES"i allreglist
whileblock: ".WHILE"i cexpr _linereminder blockstatements ".ENDW"i
WHITESPACECHARACTER: WS
smartdir: "SMART"i | "NOSMART"i
layout: layoutitem | layout layoutitem
layoutitem: WS?
%import common.SIGNED_FLOAT
